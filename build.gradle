buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/repo/" }
        maven { url "https://repo.grails.org/grails/plugins/" }
        maven { url "https://repo.grails.org/grails/core" }
        mavenCentral()
    }
    dependencies {
        classpath "com.netflix.nebula:nebula-publishing-plugin:4.4.4"
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'nebula.maven-publish' version '4.4.4'
}

version "3.0.0"
group "com.causecode.plugins"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"org.grails.grails-gsp"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    /* Default dependencies in grails 3.2.0 for plugin
    * While porting to a new grails version, create a blank plugin and replace the default dependencies with the new ones.
    */
	compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    // The commented dependencies were shipped with Grails 3.2.0 and are not required for this plugin.
	//provided "org.codehaus.groovy:groovy-ant"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.8.2"
    //runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"


    /*
    * Dependencies which were needed for the application/plugin.
    * Make sure to comment while packaging to allow Grails Mongodb plugin to install & hibernate to uninstall in parent app
    * providedCompile "org.grails.plugins:hibernate4"
    */

    compile("org.apache.jclouds.provider:cloudfiles-us:1.9.2") {
        exclude module: "jclouds-core"
        exclude module: "guice"
        exclude module: "asm"
    }
    compile("org.apache.jclouds:jclouds-compute:1.9.2") {
        exclude module: "jclouds-core"
        exclude module: "guice"
        exclude module: "asm"
    }
    compile("org.apache.jclouds.provider:aws-s3:1.9.2") {
        exclude module: "jclouds-core"
        exclude module: "guice"
        exclude module: "asm"
    }
    compile ("org.apache.jclouds:jclouds-core:1.9.2") {
        exclude module: "asm"
    }
    compile "commons-fileupload:commons-fileupload:1.3.1"
    compile "org.ow2.asm:asm:5.0.3"
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            println "Removing dependencies without a version"

            artifactId "file-uploader"

            pom.withXml {
                def pomNode = asNode()
                // version-less dependencies are handled with dependencyManagement
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username mavenPublishUser
                password mavenPublishPassword
            }
            url "http://maven-upload.causecode.com"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

grailsPublish {
    // TODO: Provide values here
    user = 'user'
    key = 'key'
    githubSlug = 'foo/bar'
    license {
        name = 'Apache-2.0'
    }
    title = "My Plugin"
    desc = "Full plugin description"
    developers = [johndoe:"John Doe"]
    portalUser = ""
    portalPassword = ""    
}
