buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "http://repo.spring.io/milestone/" }
        maven { url "https://repo.grails.org/grails/plugins" }
        maven {
            credentials {
                username mavenUser
                password mavenPassword
            }
            url "http://maven.causecode.com"
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.causecode.plugins:gradle-code-quality:0.0.8"
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

version "3.0.8"
group "com.causecode.plugins"

apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "org.grails.grails-gsp"
apply plugin: 'gradle-code-quality'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven {
        credentials {
            username mavenUser
            password mavenPassword
        }
        url "http://maven.causecode.com"
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    /* Default dependencies in grails 3.2.0 for plugin
    * While porting to a new grails version, create a blank plugin and replace the default dependencies with the new ones.
    */

    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    profile "org.grails.profiles:web-plugin"

    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"

    provided "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"

    /*
    * Dependencies which were needed for the application/plugin.
    */

    testCompile "org.grails.plugins:build-test-data:3.0.1"
    testRuntime "cglib:cglib:3.2.0"  // To resolve cglib dependency issue for test cases
    compile "com.google.code.gson:gson:2.5" // To resolve guice dependency.
    provided "org.grails.plugins:quartz:2.0.8"

    compile("org.apache.jclouds.provider:cloudfiles-us:1.9.2") {
        exclude module: "jclouds-core"
        exclude module: "guice"
        exclude module: "asm"
    }
    compile("org.apache.jclouds:jclouds-compute:1.9.2") {
        exclude module: "jclouds-core"
        exclude module: "guice"
        exclude module: "asm"
    }
    compile("org.apache.jclouds.provider:aws-s3:1.9.2") {
        exclude module: "jclouds-core"
        exclude module: "guice"
        exclude module: "asm"
    }
    compile ("org.apache.jclouds:jclouds-core:1.9.2") {
        exclude module: 'asm'
        exclude module: 'http-client'
    }
    compile("com.google.guava:guava:19.0")
    compile("com.google.cloud:google-cloud-storage:1.4.0") {
        exclude module: 'guice'
        exclude module: 'http-client'
    }

    compile "commons-fileupload:commons-fileupload:1.3.1"
    compile "org.ow2.asm:asm:5.0.3"

    provided "com.causecode.plugins:nucleus:0.4.2"
    provided "org.grails.plugins:spring-security-core:3.1.1"
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java

            artifactId "file-uploader"

            pom.withXml {
                def pomNode = asNode()
                // version-less dependencies are handled with dependencyManagement
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username mavenPublishUser
                password mavenPublishPassword
            }
            url "http://maven-upload.causecode.com"
        }
    }
}

grailsPublish {
    // TODO: Provide values here
    user = 'user'
    key = 'key'
    githubSlug = 'foo/bar'
    license {
        name = 'Apache-2.0'
    }
    title = "My Plugin"
    desc = "Full plugin description"
    developers = [johndoe:"John Doe"]
    portalUser = ""
    portalPassword = ""
}

ccJacocoExt {
    excludes = [
            '**/Application*/**',
            '**/FileUploaderTagLib*/**',
            '**/FileUploaderBootStrap*/**',
            '**/GrailsFileUploaderGrailsPlugin*/**',
            '**/UrlMappings*/**',
            '**/*Exception*/**',
            '**/DailyJob*/**',
    ]
}

jar {
    exclude('**/UrlMappings*')
}

